Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LLDT_Skills_All("Shout_LLDT_OpenMenu");
DB_LLDT_Skills_All("Target_LLDT_GenerateTradeTreasure");
DB_LLDT_Skills_All("Target_LLDT_ClearTradeTreasure");
DB_LLDT_Skills_All("Target_LLDT_ClearItems");
DB_LLDT_Skills_All("Target_LLDT_GenGold");
DB_LLDT_Skills_All("Target_LLDT_LevelUp");
DB_LLDT_Skills_All("Shout_LLDT_LevelUpParty");
DB_LLDT_Skills_All("Target_LLDT_TeleportSelf");
DB_LLDT_Skills_All("Target_LLDT_TeleportParty");

DB_LLDT_Skills_Essentials("Shout_LLDT_OpenMenu");
DB_LLDT_Skills_Essentials("Target_LLDT_GenGold");
DB_LLDT_Skills_Essentials("Target_LLDT_LevelUp");
DB_LLDT_Skills_Essentials("Shout_LLDT_LevelUpParty");
DB_LLDT_Skills_Essentials("Target_LLDT_TeleportSelf");
DB_LLDT_Skills_Essentials("Target_LLDT_TeleportParty");

DB_LLDT_Skills_Treasure("Shout_LLDT_OpenMenu");
DB_LLDT_Skills_Treasure("Target_LLDT_GenerateTradeTreasure");
DB_LLDT_Skills_Treasure("Target_LLDT_ClearTradeTreasure");
DB_LLDT_Skills_Treasure("Target_LLDT_ClearItems");
DB_LLDT_Skills_Treasure("Target_LLDT_GenGold");
KBSECTION
//REGION POINTS
IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_ATTRIBUTE", (CHARACTERGUID)_Player, _Instance)
THEN
CharacterAddAttributePoint(_Player, 1);
LLDT_Print(_Player, "+1 Attribute Point");
ObjectClearFlag(_Player, "LLDEBUGTOOLS_ADD_ATTRIBUTE");

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_ABILITY", (CHARACTERGUID)_Player, _Instance)
THEN
CharacterAddAbilityPoint(_Player, 1);
LLDT_Print(_Player, "+1 Ability Point");
ObjectClearFlag(_Player, "LLDEBUGTOOLS_ADD_ABILITY");

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_CIVIL", (CHARACTERGUID)_Player, _Instance)
THEN
CharacterAddCivilAbilityPoint(_Player, 1);
LLDT_Print(_Player, "+1 Civil Point");
ObjectClearFlag(_Player, "LLDEBUGTOOLS_ADD_CIVIL");

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_TALENT", (CHARACTERGUID)_Player, _Instance)
THEN
CharacterAddTalentPoint(_Player, 1);
LLDT_Print(_Player, "+1 Talent Point");
ObjectClearFlag(_Player, "LLDEBUGTOOLS_ADD_TALENT");

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_ALL", (CHARACTERGUID)_Player, _Instance)
THEN
CharacterAddAttributePoint(_Player, 1);
CharacterAddAbilityPoint(_Player, 1);
CharacterAddCivilAbilityPoint(_Player, 1);
CharacterAddTalentPoint(_Player, 1);
LLDT_Print(_Player, "+1 Attribute, Ability, Civil, and Talent Points");
ObjectClearFlag(_Player, "LLDEBUGTOOLS_ADD_ALL");

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_ATTRIBUTE_PARTY", (CHARACTERGUID)_Speaker, _Instance)
AND
DB_IsPlayer(_Player)
THEN
CharacterAddAttributePoint(_Player, 1);
LLDT_Print(_Player, "+1 Attribute Point");
PartyClearFlag(_Speaker, "LLDEBUGTOOLS_ADD_ATTRIBUTE_PARTY", _Instance);

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_ABILITY_PARTY", (CHARACTERGUID)_Speaker, _Instance)
AND
DB_IsPlayer(_Player)
THEN
CharacterAddAbilityPoint(_Player, 1);
LLDT_Print(_Player, "+1 Ability Point");
PartyClearFlag(_Speaker, "LLDEBUGTOOLS_ADD_ABILITY_PARTY", _Instance);

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_CIVIL_PARTY", (CHARACTERGUID)_Speaker, _Instance)
AND
DB_IsPlayer(_Player)
THEN
CharacterAddCivilAbilityPoint(_Player, 1);
LLDT_Print(_Player, "+1 Civil Point");
PartyClearFlag(_Speaker, "LLDEBUGTOOLS_ADD_CIVIL_PARTY", _Instance);

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_TALENT_PARTY", (CHARACTERGUID)_Speaker, _Instance)
AND
DB_IsPlayer(_Player)
THEN
CharacterAddTalentPoint(_Player, 1);
LLDT_Print(_Player, "+1 Talent Point");
PartyClearFlag(_Speaker, "LLDEBUGTOOLS_ADD_TALENT_PARTY", _Instance);

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_ALL_PARTY", (CHARACTERGUID)_Speaker, _Instance)
AND
DB_IsPlayer(_Player)
THEN
CharacterAddAttributePoint(_Player, 1);
CharacterAddAbilityPoint(_Player, 1);
CharacterAddCivilAbilityPoint(_Player, 1);
CharacterAddTalentPoint(_Player, 1);
LLDT_Print(_Player, "+1 Attribute, Ability, Civil, and Talent Points");
PartyClearFlag(_Speaker, "LLDEBUGTOOLS_ADD_ALL_PARTY", _Instance);
//END_REGION

//REGION DISPLAY_FLAGS
IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Player, _Instance)
AND
ObjectGetFlag(_Player, "LLDEBUGTOOLS_LOG_DISPLAYFLAGS", 1)
AND
ObjectGetFlag(_Player, _Flag, _Val)
THEN
LLDT_LogFlag(_Player, _Flag, _Val);

IF
ObjectFlagCleared(_Flag, (CHARACTERGUID)_Player, _Instance)
AND
ObjectGetFlag(_Player, "LLDEBUGTOOLS_LOG_DISPLAYFLAGS", 1)
AND
ObjectGetFlag(_Player, _Flag, _Val)
THEN
LLDT_LogFlag(_Player, _Flag, _Val);

IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Player, _Instance)
AND
ObjectGetFlag(_Player, "LLDEBUGTOOLS_LOG_PRINTFLAGS", 1)
AND
ObjectGetFlag(_Player, _Flag, _Val)
THEN
LLDT_PrintFlag(_Player, _Flag, _Val);

IF
ObjectFlagCleared(_Flag, (CHARACTERGUID)_Player, _Instance)
AND
ObjectGetFlag(_Player, "LLDEBUGTOOLS_LOG_PRINTFLAGS", 1)
AND
ObjectGetFlag(_Player, _Flag, _Val)
THEN
LLDT_PrintFlag(_Player, _Flag, _Val);
//END_REGION

//REGION LOG_SETTINGS
IF
ObjectFlagSet("LLDEBUGTOOLS_LOG_ENABLED", (CHARACTERGUID)_Player, _Instance)
THEN
SetVarFixedString(_Player, "LLDebugTools_LogText", "Log Enabled");
SetStoryEvent(_Player, "LLDT_Event_PlayerDebugLog");
//END_REGION

//REGION SKILLS
IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_SKILLS_MENU", (CHARACTERGUID)_Player, _Instance)
AND
CharacterHasSkill(_Player, "Shout_LLDT_OpenMenu", 0)
THEN
CharacterAddSkill(_Player, "Shout_LLDT_OpenMenu");
ObjectClearFlag(_Player, "LLDEBUGTOOLS_ADD_SKILLS_MENU");

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_SKILLS_ESSENTIALS", (CHARACTERGUID)_Player, _Instance)
AND
LLDT_QRY_ClearFlag(_Player, "LLDEBUGTOOLS_ADD_SKILLS_ESSENTIALS")
AND
DB_LLDT_Skills_Essentials(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 0)
THEN
CharacterAddSkill(_Player, _Skill);

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_SKILLS_TREASURE", (CHARACTERGUID)_Player, _Instance)
AND
LLDT_QRY_ClearFlag(_Player, "LLDEBUGTOOLS_ADD_SKILLS_TREASURE")
AND
DB_LLDT_Skills_Treasure(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 0)
THEN
CharacterAddSkill(_Player, _Skill);

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_SKILLS", (CHARACTERGUID)_Player, _Instance)
AND
LLDT_QRY_ClearFlag(_Player, "LLDEBUGTOOLS_ADD_SKILLS")
AND
DB_LLDT_Skills_All(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 0)
THEN
CharacterAddSkill(_Player, _Skill);

IF
ObjectFlagSet("LLDEBUGTOOLS_ADD_SKILLS_PARTY", _, _Instance)
AND
LLDT_QRY_ClearFlag(_Player, "LLDEBUGTOOLS_ADD_SKILLS_PARTY")
AND
DB_IsPlayer(_Player)
AND
DB_LLDT_Skills_All(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 0)
THEN
CharacterAddSkill(_Player, _Skill);

IF
ObjectFlagSet("LLDEBUGTOOLS_REMOVE_SKILLS", (CHARACTERGUID)_Player, _Instance)
AND
LLDT_QRY_ClearFlag(_Player, "LLDEBUGTOOLS_REMOVE_SKILLS")
AND
DB_LLDT_Skills_All(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 1)
THEN
CharacterRemoveSkill(_Player, _Skill);

IF
ObjectFlagSet("LLDEBUGTOOLS_REMOVE_SKILLS_PARTY", _, _Instance)
AND
LLDT_QRY_ClearFlag(_Player, "LLDEBUGTOOLS_REMOVE_SKILLS_PARTY")
AND
DB_IsPlayer(_Player)
AND
DB_LLDT_Skills_All(_Skill)
AND
CharacterHasSkill(_Player, _Skill, 1)
THEN
CharacterRemoveSkill(_Player, _Skill);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ZZZ_LL_DebugTools"
